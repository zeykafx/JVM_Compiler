----- Better grammar:
Program -> Declarations RecordDefinitions FunctionDefinitions .

Type -> BaseType IsList .
BaseType -> "recordNameIdentifier" | NumType | "bool" | "string" .
NumType -> "int" | "float" .
IsList -> "[]" | .

Block -> "{" Stmts ReturnStmt "}" .
Stmts -> Stmt Stmts | .
Stmt -> IfStmt | ForLoop | WhileLoop | FunctionDefinition | BaseStatement ";" .
ReturnStmt -> "return" ReturnTail .
ReturnTail -> ";" | Expression ";" .

BaseStatement -> Declaration | VariableAssignment | RecordDefinition .

Declarations -> Declaration Declarations | .

Declaration -> Final "identifier" Type Assignment . # (need to add "var" to be LL(1))
Assignment -> "=" Expression | .
Final -> "final" | .

VariableAssignment -> IdentifierAccess "=" Expression .

RecordDefinitions -> RecordDefinition RecordDefinitions | .

RecordDefinition -> "recordNameIdentifier" "rec" "{" RecordFields "}" .
RecordFields -> RecordField RecordFieldsTail | .
RecordFieldsTail -> ";" RecordField RecordFieldsTail | .
RecordField -> "identifier" Type .

FunctionDefinitions -> FunctionDefinition FunctionDefinitions | .
FunctionDefinition -> "fun" "identifier" "(" Params ")" Type Block .
# OLD: FunctionDefinition -> "fun" "identifier" "(" Params ")" Type Block
# NEW:
FunctionDefinition -> "fun" InstanceRef "identifier" "(" Params ")" Type Block .
InstanceRef ->  "(" "identifier" "recordNameIdentifier" ")" | .

Params -> Param ParamsTail | .
ParamsTail -> "," Param ParamsTail | .
Param -> "identifier" Type .

IfStmt -> "if" "(" Expression ")" Block ElseStmt .
ElseStmt -> "else" Block | .

WhileLoop -> "while" "(" Expression ")" Block .
ForLoop -> "for" "(" ForCondition ")" Block .
# OLD : ForCondition -> "identifier" "," Term "," Term "," Term .
# NEW: changed to allow for methods on records
ForCondition -> "identifier" "," Expression "," Expression "," Expression .


IdentifierAccess -> "identifier" AccessChain .
AccessChain -> Access AccessChain | .
Access -> "[" Expression "]" | "." "identifier" AccessTail .
AccessTail -> "(" ParamsCall ")" | .  # NEW : changed to allow for methods on records

Expression -> Term IsBinaryExpr | ArrayExpression | UnaryOperator Term .
IsBinaryExpr -> BinaryOperator Term | .

ArrayExpression -> "array" "[" "intval" "]" "of" Type ";" .

NewRecord -> "recordNameIdentifier" "(" ParamsCall ")" . # Note: record identifiers start with a capital letter

Term -> "(" Expression ")" | IdentifierOrFunctionCall | NewRecord | ConstVal .
IdentifierOrFunctionCall -> "identifier" IdentifierOrFunctionCallTail .
IdentifierOrFunctionCallTail -> | "(" ParamsCall ")" .

ParamsCall -> Expression MoreParamsCall .
MoreParamsCall ->  | "," ParamsCall .

UnaryOperator -> "!" | "-" .
BinaryOperator -> "+" | "-" | "*" | "/" | "%" | "&&" | "||" | "==" | "!=" | "<" | ">" | "<=" | ">=" .
ConstVal -> "intval" | "floatval" | "boolval" | "stringval" | "true" | "false" .
